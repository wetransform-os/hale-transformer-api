plugins {
	id 'java'
	id 'org.springframework.boot' version '3.1.5'
	id 'io.spring.dependency-management' version '1.1.3'
	id 'com.diffplug.spotless' version '6.23.0'
}

group = 'to.wetransform.hale'
version = '0.0.1-SNAPSHOT'

java {
	sourceCompatibility = JavaVersion.VERSION_17
	targetCompatibility = JavaVersion.VERSION_17
}

repositories {
	// mavenLocal() //XXX for testing
	maven {
		// wetransform release repository (hale releases and Eclipse dependencies)
		url 'https://artifactory.wetransform.to/artifactory/local'
	}
	// this needs to be defined before jcenter/MavenCentral for retrieving JAI
	maven {
		url 'https://repo.osgeo.org/repository/release/'
	}
	mavenCentral()
}

project.ext {
	haleVersion = '5.1.0-SNAPSHOT'
	cliVersion = '5.1.0-SNAPSHOT'
	groovyVersion = '2.5.19'
}

dependencies {
	// Spring
	implementation 'org.springframework.boot:spring-boot-starter-actuator'
	implementation 'org.springframework.boot:spring-boot-starter-quartz'
	implementation 'org.springframework.boot:spring-boot-starter-web'
	implementation 'org.springframework.boot:spring-boot-starter-amqp'
	implementation 'org.apache.httpcomponents.client5:httpclient5:5.2.1'

	// hale
	implementation 'eu.esdihumboldt.unpuzzled:org.eclipse.equinox.nonosgi.registry:1.0.0'
	implementation "to.wetransform:hale-cli:$cliVersion", {
		/*
		 * XXX The dependencies introduced by the schematron bundle cause some problems.
		 */
		exclude group: 'eu.esdihumboldt.hale', module: 'eu.esdihumboldt.hale.io.schematron'
	}

	implementation "eu.esdihumboldt.hale:eu.esdihumboldt.hale.app.cli.commands:$haleVersion"
	implementation "org.codehaus.groovy:groovy-all:$groovyVersion"
	implementation 'org.json:json:20190722'
	implementation 'org.slf4j:jul-to-slf4j:1.7.21'
	implementation 'org.apache.httpcomponents:httpmime:4.5.14'

	//developmentOnly 'org.springframework.boot:spring-boot-docker-compose'
	testImplementation 'org.springframework.boot:spring-boot-starter-test'

	// Logging
	testRuntimeOnly 'ch.qos.logback:logback-core:1.4.11'
	testRuntimeOnly 'ch.qos.logback:logback-classic:1.4.11'
}

configurations.all {
	// ensure SNAPSHOTs are updated every time if needed
	resolutionStrategy.cacheChangingModulesFor 0, 'seconds'
}

tasks.named('test') {
	useJUnitPlatform()
}

spotless {
	java {
		palantirJavaFormat()
		importOrder('java', 'javax', '')
		removeUnusedImports()
		indentWithSpaces(4)
		trimTrailingWhitespace()
		endWithNewline()

		target 'src/*/java/**/*.java'
	}
	groovyGradle {
		target '*.gradle' // default target of groovyGradle
	}
}

/*
 * Gradle wrapper
 */
wrapper {
	gradleVersion = '8.4'
}
